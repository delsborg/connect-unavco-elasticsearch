PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:   <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX ec: <https://library.ucar.edu/earthcollab/schema#>
PREFIX cito: <http://purl.org/spar/cito/>
PREFIX obo:	<http://purl.obolibrary.org/obo/>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>

DESCRIBE ?grant ?mostSpecificType ?label ?dtInt ?dtStart ?role ?investigator ?researchArea ?investigatorOrganization ?dataType ?awardedBy ?org ?vcardName
WHERE {
  ?grant vitro:mostSpecificType ?mostSpecificType .
  ?grant rdfs:label ?label .
  OPTIONAL { 
      ?grant vivo:dateTimeInterval ?dtInt .
      ?dtInt vivo:start ?dtStart .
      ?dtStart vivo:dateTime ?startDate . }
  OPTIONAL { {
    ?grant vivo:relates ?role .
    ?role a vivo:InvestigatorRole . 
    ?role obo:RO_0000052 ?investigator .
    ?investigator a foaf:Person . }
    UNION {
       ?grant vivo:relates ?role .
       ?role a vivo:InvestigatorRole . 
       ?role obo:RO_0000052 ?investigator .
       ?investigator a vcard:Individual .
       ?investigator vcard:hasName ?vcardName . } 
    }
  OPTIONAL { 
      ?grant vivo:relates ?org .
      ?org a foaf:Organization . }
  OPTIONAL { ?grant vivo:assignedBy ?awardedBy }
}
