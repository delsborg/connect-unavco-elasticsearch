PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:   <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX vitro-pub: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX ec: <https://library.ucar.edu/earthcollab/schema#>
PREFIX cito: <http://purl.org/spar/cito/>
PREFIX obo:	<http://purl.obolibrary.org/obo/>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX vlocal: <http://connect.unavco.org/ontology/vlocal#>

DESCRIBE ?station ?mostSpecificType ?label ?dt ?role ?investigator ?relDataset ?vcardName ?4chid ?place ?main_image ?thumb_image ?thumbnail
WHERE {
  ?station vitro:mostSpecificType ?mostSpecificType .
  ?station rdfs:label ?label .
  OPTIONAL { 
      ?station vivo:dateTimeValue ?dt .
      ?dt vivo:dateTime ?retireDate . }
  OPTIONAL { {
    ?station vivo:relates ?role .
    ?role a vivo:InvestigatorRole . 
    ?role obo:RO_0000052 ?investigator .
    ?investigator a foaf:Person . }
    UNION {
       ?station vivo:relates ?role .
       ?role a vivo:InvestigatorRole . 
       ?role obo:RO_0000052 ?investigator .
       ?investigator a vcard:Individual .
       ?investigator vcard:hasName ?vcardName . } 
    }
  OPTIONAL { ?station obo:RO_0002234 ?relDataset }
  OPTIONAL { ?station vlocal:has4CharID ?4chid }
  OPTIONAL { ?station obo:BFO_0000050 ?place }
  OPTIONAL { ?station vitro-pub:mainImage ?main_image .
             ?main_image vitro-pub:thumbnailImage ?thumb_image .
             ?thumb_image vitro-pub:downloadLocation ?thumbnail }
}
