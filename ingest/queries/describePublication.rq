PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:   <http://www.w3.org/2002/07/owl#>
PREFIX dco: <http://info.deepcarbon.net/schema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX ec: <https://library.ucar.edu/earthcollab/schema#>
PREFIX cito: <http://purl.org/spar/cito/>
PREFIX obo:	<http://purl.obolibrary.org/obo/>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>

DESCRIBE ?publication ?dcoid ?mostSpecificType ?subjectArea ?venue ?event ?community ?team ?authorship ?author ?researchArea ?vcardName ?publicationYear ?publicationObj ?citation
  WHERE { 
  ?publication vitro:mostSpecificType ?mostSpecificType .
  ?publication rdfs:label ?label .
  OPTIONAL { 
      ?publication vivo:dateTimeValue ?publicationObj .
      ?publicationObj vivo:dateTime ?publicationYear . }
  OPTIONAL { ?publication bibo:presentedAt ?event }
  OPTIONAL { ?publication vivo:hasPublicationVenue ?venue }
  OPTIONAL { ?publication vivo:hasSubjectArea ?subjectArea }
  OPTIONAL { {
    ?publication vivo:relatedBy ?authorship .
    ?authorship a vivo:Authorship . 
    ?authorship vivo:relates ?author .
    ?author a foaf:Person . }
    UNION {
       ?publication vivo:relatedBy ?authorship .
       ?authorship a vivo:Authorship . 
       ?authorship vivo:relates ?author .
       ?author a vcard:Individual .
       ?author vcard:hasName ?vcardName . } 
    }
  OPTIONAL { ?publication cito:citesAsDataSource ?citation }
}
